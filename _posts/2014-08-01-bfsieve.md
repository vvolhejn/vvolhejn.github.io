---
layout: single
title:  "Sieve of Erathostenes in Brainf*ck"
date:   2014-08-01 00:00:00 +0100
categories:
header:
  teaser: /assets/images/bfsieve-thumbnail.png
---

I got really into esoteric languages at some point, which culminated
in me writing the Sieve of Erathostenes in [Brainf*ck](https://en.wikipedia.org/wiki/Brainfuck),
a programming language with only 8 commands.
**Run the code [here](https://copy.sh/brainfuck/?c=VGhlIFNpZXZlIG9mIEVyYXRvc3RoZW5lcyBpbiBCcmFpbmZ1Y2sKKEkgdXNlIHNlbWljb2xvbnMgaW5zdGVhZCBvZiBjb21tYXMgYW5kIHBlcmlvZHMgc28gdGhhdCBJIGRvbid0IGJyZWFrIHRoZSB2YWxpZGl0eSBvZiB0aGUgY29kZSkKVGhlIHByb2dyYW0gcHJpbnRzIHByaW1lcyB1cCB0byBOOyB3aGljaCBpdCByZWFkcyBmcm9tIGlucHV0ICh0ZXJtaW5hdGVkIGJ5IDApCk1lbW9yeSBjZWxscyBhcmUgZGl2aWRlZCBpbnRvIGdyb3VwcyBvZiBmaXZlOyBlYWNoIGdyb3VwIHJlcHJlc2VudHMgb25lIG51bWJlcgpFYWNoIGNlbGwgb2YgdGhlIGdyb3VwIGhhcyBhIGNlcnRhaW4gZnVuY3Rpb246CjE6IFRoZSBudW1iZXIgaXRzZWxmCjI6IE51bWJlciBvZiBkaXZpc29ycyBvZiB0aGlzIG51bWJlciBtaW51cyAyOyB0aGlzIHZhbHVlIGlzIDAgZm9yIHByaW1lcyBhbmQgbW9yZSBmb3IgY29tcG9zaXRlIG51bWJlcnMKMzogV2hldGhlciB0aGUgbnVtYmVyIGlzIGluIHJhbmdlIGZyb20gMSB0byBOOyAxIGlmIHllczsgMCBpZiBubwo0OiBUZW1wb3JhcnkgdmFsdWUKNTogVGVtcG9yYXJ5IHZhbHVlClRIRSBQUk9HUkFNIElUU0VMRgo-Pj4-Pj4-Pj4-Pj4-ClByaW50ICJFbnRlciBhIG51bWJlcjoiIGFuZCByZWFkIGlucHV0CisrKysrKysrKysrWy0-KysrKysrPisrKysrKysrKys-KysrKysrKysrPisrKzw8PDxdPisrKy4-LisrKysKKysuPisrLjwtLS4-Pi0uPC0tLS0uPi48PC0tLS0uKysrKysrKy4tLS0tLS0tLS4-Ky4rKysuPCsrKysrLgo8LS0tLS0tLS0tLS0uPj4-LlstXTxbLV08Wy1dPFstXStbWy1dPlstXSxbK1stLS0tLS0tLS0tLVs-Wy1dCisrKysrK1stPC0tLS0tLT5dPC0tPDxbLT4-KysrKysrKysrKzw8XT4-Wy08PCs-Pl08Kz5dXV08XTwKSW5pdGlhbGl6YXRpb24KWy0-Kz4rPDxdPj5bLTw8Kz4-XTwtCls8PCs-CiAgICBbLT4-Pj4rPis8PDw8PF0KICAgID4-Pj5bLTw8PDwrPj4-Pl08PDwKICAgIFstPj4-Pj4rPDw8PDxdPj4-Pj4tCl08PCs-CkFzc2lnbiBudW1iZXJzIHRvIHRoZSBncm91cHMKWy08PDwrPj4-XTw8PC0KWys8PFstXT4-Wy08Kzw8PDwrPj4-Pj5dPFstPis8XTw8PDwtLV0KPj4tPj4-Cj4-Wwo8Wy0-Pis-Kzw8PF0-Pj5bLTw8PCs-Pj5dKzwKW1stXT5bLV08XQo-W1stXQpDb3B5IGRhdGEgZnJvbSBjdXJyZW50IG51bWJlcgpNb3ZpbmcgdG8gdGhlICJpbmRleCIgY2VsbCBpbiB0aGUgZ3JvdXAKPDw8PApbLT4rPj4rPis8PDw8XT5bLTwrPl0-Cls-CiAgICBbIFdlIGhhdmVuJ3QganVtcGVkIGVub3VnaAogICAgICAgIFstPj4-Pj4rPDw8PDxdPj4-Pj4tPDw8PDwKICAgICAgICA-Wy0-Pj4-Pis8PDw8PF08PC0-CiAgICBdPFsgTWFyayBhcyBjb21wb3NpdGUKICAgICAgICA8Kz4tPj5bLTwrPCs-Pl08PFstPj4rPDxdPgogICAgICAgIFstPj4-Pj4rPDw8PDxdPj4-Pj4tPDw8PDwKICAgICAgICA-Wy0-Pj4-Pis8PDw8PF08CiAgICAgICAgPAogICAgXQogICAgWy1dKz4-Pj4-Cl0KR28gYmFjawo-Wy0-LTxdPitbWy08PDw8PCs-Pj4-Pl08PDw8PC1dCjw8Wzw8PDw8XT4-Pj4-Pj5dCjw8CkdvIHRvIG5leHQgbnVtYmVyClstXT4-Pj4-Cl08PApPdXRwdXQgKHByaW50aW5nIEFTQ0lJIGFzIG51bWJlcnMgYWRhcHRlZCBmcm9tIGVsc2V3aGVyZSkKPDw8PDxbPDw8PDxdPj4-Pj4KWz4-Wy1dKzxbWy1dPi08XT5bCjw8CjxbLV08Wy1dPFstXTxbLV08Wy1dPFstXTxbLV08Wy1dPFstXSsrKysrKysrKysuWy1dIE5ld2xpbmUgYmVmb3JlIHRoZSBudW1iZXIKPj4-Pj4-PgorKysrKysrKysrPj5bLTwrPC1bPCs8PF08WytbLT4rPF08Kzw8XT4-Pj4-Pl08PFstXTw8PCsrKysrKysrKys-Wy08LVs8Kzw8XTxbK1stID4rPF08Kzw8XT4-Pj4-XTxbLV08PFs8KysrKysrWy0-KysrKysrKys8XT4uPj4rPCs8Wy1dXT5bPlstPC0-XSsrKysrK1stPCsrKysrKysrPl08LlstXV0-PisrKysrK1stPisrKysrKysrPF0-LlstXT4-Wy0-KzxdPgo-Cj4tXQo-Pj5d)**
or [view on GitHub](https://github.com/vvolhejn/bfsieve/blob/master/sieve.commented.bf).

The GitHub README says "I wrote this in the summer of 2014 when I was bored on holiday. The whole thing was written from my phone."
Weird flex, teenage me.

The program prints primes up to N, which it reads from input (terminated by 0).
Memory cells are divided into groups of five, each group represents one number.
Each cell of the group has a certain function:
1. The number itself
2. Number of divisors of this number minus 2. This value is 0 for primes and more for composite numbers
3. Whether the number is in range from 1 to N, 1 if yes, 0 if no
4. Temporary value
5. Temporary value


{% highlight brainfuck %}
>>>>>>>>>>>>>
Print "Enter a number:" and read input
+++++++++++[->++++++>++++++++++>+++++++++>+++<<<<]>+++.>.++++
++.>++.<--.>>-.<----.>.<<----.+++++++.--------.>+.+++.<+++++.
<-----------.>>>.[-]<[-]<[-]<[-]+[[-]>[-],[+[-----------[>[-]
++++++[-<------>]<--<<[->>++++++++++<<]>>[-<<+>>]<+>]]]<]<
Initialization
[->+>+<<]>>[-<<+>>]<-
[<<+>
    [->>>>+>+<<<<<]
    >>>>[-<<<<+>>>>]<<<
    [->>>>>+<<<<<]>>>>>-
]<<+>
Assign numbers to the groups
[-<<<+>>>]<<<-
[+<<[-]>>[-<+<<<<+>>>>>]<[->+<]<<<<--]

>>->>>
>>[
<[->>+>+<<<]>>>[-<<<+>>>]+<
[[-]>[-]<]
>[[-]
Copy data from current number
Moving to the "index" cell in the group
<<<<
[->+>>+>+<<<<]>[-<+>]>
[>
    [ We haven't jumped enough
        [->>>>>+<<<<<]>>>>>-<<<<<
        >[->>>>>+<<<<<]<<->
    ]<[ Mark as composite
        <+>->>[-<+<+>>]<<[->>+<<]>
        [->>>>>+<<<<<]>>>>>-<<<<<
        >[->>>>>+<<<<<]<
        <
    ]
    [-]+>>>>>
]
Go back
>[->-<]>+[[-<<<<<+>>>>>]<<<<<-]
<<[<<<<<]>>>>>>>]
<<
Go to next number
[-]>>>>>
]<<
Output (printing ASCII as numbers adapted from elsewhere)
<<<<<[<<<<<]>>>>>
[>>[-]+<[[-]>-<]>[
<<
<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]++++++++++.[-] Newline before the number
>>>>>>>
++++++++++>>[-<+<-[<+<<]<[+[->+<]<+<<]>>>>>>]<<[-]<<<++++++++++>[-<-[<+<<]<[+[- >+<]<+<<]>>>>>]<[-]<<[<++++++[->++++++++<]>.>>+<+<[-]]>[>[-<->]++++++[-<++++++++>]<.[-]]>>++++++[->++++++++<]>.[-]>>[->+<]>
>
>-]
>>>]
{% endhighlight %}
